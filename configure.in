AC_INIT(src/Game.cpp)
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE(glob2, 0.8.2)
AM_CONFIG_HEADER(config.h)

CXXFLAGS="-g -Wall $CXXFLAGS"

dnl Programs 
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_RANLIB


dnl Set to C for all libraries test
AC_LANG(C)


dnl Header files
AC_STDC_HEADERS
AC_HAVE_HEADERS(string.h sys/time.h assert.h)


dnl Libraries

dnl Check for SDL
SDL_VERSION=1.2.0
AM_PATH_SDL($SDL_VERSION, :, AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!]))

dnl Check for SDL_image and SDL_net
AC_CHECK_LIB(SDL_image, IMG_Load_RW, SDL_LIBS="$SDL_LIBS -lSDL_image", AC_MSG_ERROR([*** SDL_image not found !]))
AC_CHECK_LIB(SDL_net, SDLNet_UDP_Open, SDL_LIBS="$SDL_LIBS -lSDL_net", AC_MSG_ERROR([*** SDL_net not found !]))

dnl Check for FT2
AC_CHECK_FT2

dnl Check for libvorbis
XIPH_PATH_VORBIS

dnl Open GL support
AC_ARG_ENABLE(opengl, [  --enable-opengl        Enable the OpenGL renderer if available (default is disabled)], enable_opengl="yes", enable_opengl="no")
if test "x$enable_opengl" = "xyes"; then
	AC_CHECK_LIB(GL, glBegin)
	AC_CHECK_LIB(GLU, gluOrtho2D)
fi


dnl Check for header files
AC_FUNC_ALLOCA
AC_HEADER_DIRENT


dnl Set the C++ after the tests
AC_LANG(C++)

AC_CHECK_HEADERS(vector,,AC_MSG_WARN(STL classes missing ?))


dnl Packaging
dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}", [data directory])
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}", [data directory])
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}", [data directory])
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}", [source directory])

AC_OUTPUT([Makefile src/Makefile doc/Makefile data/Makefile 
data/gfx/Makefile
data/fonts/Makefile
data/gui/Makefile
data/zik/Makefile
maps/Makefile
libgag/Makefile
libgag/include/Makefile
libgag/src/Makefile
] )

