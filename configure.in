AC_PREREQ(2.59)
AC_INIT([Globulation 2], 0.8.11, [glob2-devel@nongnu.org], glob2)
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE(glob2, 0.8.11)

AC_CONFIG_SRCDIR([src/Game.cpp])
AM_CONFIG_HEADER(config.h)

CXXFLAGS="$CXXFLAGS -Wall -g"

dnl Programs 
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_RANLIB

dnl Set to C for all libraries test
AC_LANG(C)

dnl Header files
AC_STDC_HEADERS
AC_FUNC_ALLOCA
AC_HEADER_DIRENT
AC_HAVE_HEADERS(string.h sys/time.h assert.h)

dnl Libraries

dnl Check for Speex
AC_CHECK_HEADER(speex.h,,AC_MSG_ERROR([*** Speex header not found. Please install Speex (http://www.speex.org).]))
AC_CHECK_LIB(speex,speex_encoder_init,,AC_MSG_ERROR([*** Speex library not found. Please install Speex (http://www.speex.org).]))

dnl Check for SDL
SDL_VERSION=1.2.0
AM_PATH_SDL($SDL_VERSION, :, AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!]))

dnl Check for SDL_image and SDL_net
AC_CHECK_LIB(SDL_image, IMG_Load_RW, SDL_LIBS="$SDL_LIBS -lSDL_image", AC_MSG_ERROR([*** SDL_image not found !]), $SDL_LIBS)
AC_CHECK_LIB(SDL_net, SDLNet_UDP_Open, SDL_LIBS="$SDL_LIBS -lSDL_net", AC_MSG_ERROR([*** SDL_net not found !]), $SDL_LIBS)

dnl Check for FT2
AC_CHECK_FT2

dnl Check for libvorbis
XIPH_PATH_VORBIS

dnl Open GL support
have_opengl=yes
AC_ARG_ENABLE(opengl, [  --disable-opengl        Disable the OpenGL renderer even if available (default is enabled)],have_opengl=no,)
if test "$have_opengl" = "yes"; then
	AC_CHECK_HEADER(GL/gl.h,,have_opengl=no)
	AC_CHECK_HEADER(GL/glu.h,,have_opengl=no)
	AC_CHECK_LIB(GL, glBegin,,have_opengl=no,-lpthread)
	AC_CHECK_LIB(GLU, gluOrtho2D,,have_opengl=no,-lpthread)
fi

if test "$have_opengl" = "yes"; then
	AC_DEFINE([HAVE_OPENGL],[],[Defined when OpenGL support is present and compiled])
fi

dnl Type of voice recorder
AC_DEFINE([AUDIO_RECORDER_OSS],,[Set the audio input type to OSS; the UNIX Open Sound System])

dnl Set the C++ after the tests
AC_LANG(C++)

AC_CHECK_HEADERS(vector,,AC_MSG_WARN(STL classes missing ?))


dnl Packaging
dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}", [data directory])
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}", [data directory])
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}", [data directory])
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}", [source directory])

AC_CONFIG_FILES([Makefile
	src/Makefile
	doc/Makefile
	data/Makefile
	data/gfx/Makefile
	data/gfx/cursor/Makefile
	data/fonts/Makefile
	data/gui/Makefile
	data/zik/Makefile
	maps/Makefile
	campaigns/Makefile
	libgag/Makefile
	libgag/include/Makefile
	libgag/src/Makefile
	gnupg/Makefile])
AC_OUTPUT

